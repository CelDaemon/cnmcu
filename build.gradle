import tools.elmfer.*

plugins {
	id "fabric-loom" version "1.14-SNAPSHOT"
	id "maven-publish"
	id "com.gradleup.shadow" version "9.3.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {

	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	
    runtimeOnly shadow("io.github.spair:imgui-java-binding:${project.imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-lwjgl3:${project.imgui_version}") {
		exclude(group: "org.lwjgl") // Do not include transitive LWJGL3 dependency.
	}

	runtimeOnly shadow("io.github.spair:imgui-java-natives-windows:${project.imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-natives-linux:${project.imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-natives-macos:${project.imgui_version}")
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}
jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName}"}
	}
}

clean {
	delete fileTree("src/main/cpp/bridge") {
		include "*.h"
		include "*.cpp"
		exclude "cnmcuJava.cpp"
		exclude "cnmcuJava.h"
	}
}

tasks.register("genNativeSources", GenNativeSourcesTask) {
	dependsOn assemble
	
	sourceDir = file("src/main/java")
	bridgeDir = file("src/main/cpp/bridge")
	
}

tasks.register("compileNatives", CompileNativesTask) {
	dependsOn genNativeSources
	
	sourceDir = file("src/main/cpp")
	buildDir = layout.buildDirectory.dir("natives")
	targetDir = file("run/cnmcu/natives/${project.property("mod_version")}")
}

tasks.register("saveVersion", SaveVersionTask)

shadowJar {
	configurations = [project.configurations.shadow]
}

remapJar {
	input = tasks.shadowJar.archiveFile
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
