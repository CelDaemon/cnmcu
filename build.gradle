import tools.elmfer.*

plugins {
	id "fabric-loom" version "${loom_version}"
	id "com.gradleup.shadow" version "9.3.0"
}

version = "${mod_version}+${minecraft_version}"
group = maven_group

base {
	archivesName = archives_base_name
}

loom.log4jConfigs.from "log4j-dev.xml"

repositories {
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	
    runtimeOnly shadow("io.github.spair:imgui-java-binding:${imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-lwjgl3:${imgui_version}") {
		exclude(group: "org.lwjgl") // Do not include transitive LWJGL3 dependency.
	}

	runtimeOnly shadow("io.github.spair:imgui-java-natives-windows:${imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-natives-linux:${imgui_version}")
	runtimeOnly shadow("io.github.spair:imgui-java-natives-macos:${imgui_version}")
}

tasks.withType(JavaCompile).configureEach {
	options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}
jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName}"}
	}
}

var genNativeSources = tasks.register("genNativeSources", GenNativeSourcesTask) {
	sourceDir = sourceSets.main.java.sourceDirectories.first()
}

var compileNatives = tasks.register("compileNatives", CompileNativesTask) {
	inputs.property "version", version
	sourceDir = file("src/main/cpp")
	bridgeDir.set(genNativeSources.<Directory>flatMap {it.bridgeDir })
	buildDir = layout.buildDirectory.dir("tmp/${name}")
	outputDir = layout.buildDirectory.dir("natives")
	targetDir = layout.projectDirectory.dir(loom.runs.client.runDir)
			.dir("cnmcu/natives/${version}")

}

var prebuiltProvider = project.provider { layout.projectDirectory.dir(project.property("prebuilt_natives") as String) }
var compiledProvider = compileNatives.<Directory>flatMap { it.outputDir }

var nativesProvider = project.hasProperty("prebuilt_natives") ? prebuiltProvider : compiledProvider

processResources {
	inputs.files "natives", nativesProvider
	inputs.property "version", version
	inputs.property "minecraft_version", minecraft_version
	inputs.property "loader_version", loader_version

	filesMatching("fabric.mod.json") {
		expand "version": version,
				"minecraft_version": minecraft_version,
				"loader_version": loader_version
	}

	from(nativesProvider) {
		into("com/elmfer/cnmcu/config")
	}
}

tasks.register("saveVersion", SaveVersionTask)

shadowJar {
	configurations = [project.configurations.shadow]
}

remapJar {
	input = tasks.shadowJar.archiveFile
}
